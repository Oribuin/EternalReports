import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.0'
    //id 'me.bristermitten.pdm' version '0.0.28'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

group 'xyz.oribuin'
version '1.0'
compileJava.options.encoding = 'UTF-8'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

// Repos
repositories {
    mavenCentral()
    mavenLocal()
    jcenter()

    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'http://repo.oribuin.xyz:8081/repository/maven-public/' }
    maven { url 'http://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://repo.codemc.org/repository/maven-public' }
    maven { url 'https://repo.rosewooddev.io/repository/public/' }
}

// Dependencies
dependencies {
    // Overlord language
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // Spigot Stuff
    shadow 'org.spigotmc:spigot:1.16.4'
    shadow 'me.clip:placeholderapi:2.10.9'

    implementation 'xyz.oribuin:orilib:1.0.6'
    implementation 'dev.rosewood:guiframework:1.1.1'
    implementation 'com.zaxxer:HikariCP:3.4.5'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

// Temporary replacement for PDM
shadowJar {
    archiveClassifier.set(null)

    // Relocate so much shit
    relocate ('org.jetbrains', 'xyz.oribuin.eternalreports.libs.jetbrains')
    relocate ('org.intellij', 'xyz.oribuin.eternalreports.libs.intellij')
    relocate ('kotlin', 'xyz.oribuin.eternalreports.libs.kotlin')
    relocate ('dev.rosewood.guiframework', 'xyz.oribuin.eternalreports.libs.guiframework')
    relocate ('xyz.oribuin.orilibrary', 'xyz.oribuin.eternalreports.library')
}


//jar.dependsOn project.tasks.getByName('pdm')
build.dependsOn shadowJar